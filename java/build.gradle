plugins {
    id 'com.diffplug.spotless' version '5.11.1' apply false
}

subprojects {
    pluginManager.withPlugin('java') {
        apply plugin: 'com.diffplug.spotless'

        java {
            toolchain {
                languageVersion = JavaLanguageVersion.of(11)
            }
        }

        spotless {
            java {
                googleJavaFormat('1.9')
            }
        }

        repositories {
          mavenCentral()
          maven { url 'https://oss.sonatype.org/content/repositories/snapshots' }
        }

        test {
            useJUnitPlatform()
        }

        dependencies {
            implementation platform("io.opentelemetry:opentelemetry-bom:${project['otel.version']}")
            implementation platform("io.opentelemetry:opentelemetry-bom-alpha:${project['otel-alpha.version']}")
            implementation platform("io.opentelemetry.instrumentation:opentelemetry-instrumentation-bom-alpha:${project['otel-instrumentation.version']}")

            testImplementation 'org.junit.jupiter:junit-jupiter-api:5.7.0'
            testRuntimeOnly 'org.junit.jupiter:junit-jupiter-engine:5.7.0'
        }
    }
}