plugins {
    id 'com.diffplug.spotless' apply false
}

subprojects {
    pluginManager.withPlugin('java') {
        apply plugin: 'com.diffplug.spotless'

        java {
            toolchain {
                languageVersion = JavaLanguageVersion.of(11)
            }
        }

        spotless {
            java {
                googleJavaFormat('1.9')
            }
        }

        repositories {
          mavenCentral()
          // Uncomment to access snapshots
          // maven { url 'https://oss.sonatype.org/content/repositories/snapshots' }
        }

        test {
            useJUnitPlatform()
        }

        dependencies {
            implementation platform("io.opentelemetry:opentelemetry-bom:1.22.0")
            implementation platform("io.opentelemetry:opentelemetry-bom-alpha:1.22.0-alpha")
            implementation platform("io.opentelemetry.instrumentation:opentelemetry-instrumentation-bom-alpha:1.21.0-alpha")

            testImplementation 'org.junit.jupiter:junit-jupiter-api:5.9.1'
            testRuntimeOnly 'org.junit.jupiter:junit-jupiter-engine:5.9.1'
        }
    }
}