import org.springframework.boot.gradle.plugin.SpringBootPlugin

plugins {
  id 'org.springframework.boot'
  id 'java'
  id 'com.diffplug.spotless'
}

java {
  toolchain {
    languageVersion = JavaLanguageVersion.of(17)
  }
}

spotless {
  java {
    googleJavaFormat()
  }
}

repositories {
  mavenCentral()
}

bootRun {
  mainClass.set 'com.example.demo.Application'
}

configurations.all {
  exclude module: 'spring-boot-starter-logging'
}

dependencies {
  implementation platform(SpringBootPlugin.BOM_COORDINATES)
  implementation 'org.springframework.boot:spring-boot-starter-web'
  implementation 'org.springframework.boot:spring-boot-starter-log4j2'

  // OpenTelemetry core
  implementation platform('io.opentelemetry:opentelemetry-bom:1.38.0')
  implementation 'io.opentelemetry:opentelemetry-api'
  implementation 'io.opentelemetry:opentelemetry-sdk'
  implementation 'io.opentelemetry:opentelemetry-exporter-otlp'
  implementation 'io.opentelemetry:opentelemetry-sdk-extension-autoconfigure'

  // OpenTelemetry instrumentation
  implementation platform('io.opentelemetry.instrumentation:opentelemetry-instrumentation-bom-alpha:2.3.0-alpha')
  implementation 'io.opentelemetry.instrumentation:opentelemetry-runtime-telemetry-java8'
  implementation 'io.opentelemetry.instrumentation:opentelemetry-log4j-appender-2.17'
  implementation 'io.opentelemetry.instrumentation:opentelemetry-spring-webmvc-6.0'
}
