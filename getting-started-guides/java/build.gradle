import org.springframework.boot.gradle.plugin.SpringBootPlugin

plugins {
  id 'java'
  id 'com.diffplug.spotless' version '6.25.0'
  id 'org.springframework.boot' version '3.2.5'
}

java {
  toolchain {
    languageVersion = JavaLanguageVersion.of(17)
  }
}

spotless {
  java {
    googleJavaFormat()
  }
}

repositories {
  mavenCentral()
  maven {
    url 'https://artifacts.datanerd.us/newrelic-release'
  }
}

configurations {
  // Create a new configuration to hold the otel java agent
  agent
}

dependencies {
  implementation platform(SpringBootPlugin.BOM_COORDINATES)
  implementation 'org.springframework.boot:spring-boot-starter-web'

  implementation "com.newrelic.opentelemetry:opentelemetry-common:3.22.0"

  // Add OpenTelemetry java agent to the "agent" configuration we previously defined
  agent("com.newrelic.agent.java:newrelic-agent:8.12.0")
}

tasks.register("copyAgent", Copy) {
  from(configurations.agent.singleFile)
  into(layout.buildDirectory.dir('agent'))
  rename("newrelic-agent-.*\\.jar", "newrelic-agent.jar")
}

bootJar {
  archiveBaseName.set('getting-started-java')
  // Before running, copy the agent to a reliable place in the build dir
  dependsOn("copyAgent")
}

bootRun {
  // Before running, copy the agent to a reliable place in the build dir
  dependsOn("copyAgent")

  mainClass.set 'com.example.demo.Application'

  def agentPath = project.buildDir.toString() + "/agent/newrelic-agent.jar"
  jvmArgs = [
    // Set the opentelemetry-java-instrumentation agent as the javaagent
    "-javaagent:${agentPath}"
  ]

}
